# lexd ani_verbs.lexd | hfst-txt2fst -o ani_verbs.lexd.hfst
# hfst-fst2strings ani_verbs.lexd.hfst
# hfst-twolc new.twol -o new.twol.hfst
# hfst-compose-intersect ani_verbs.lexd.hfst new.twol.hfst -o new.generator.hfst
# hfst-fst2strings new.generator.hfst


PATTERNS
Verb

#-----------VERBS-----------

PATTERN Verb
Class Auxilary [<aux>:] > [<perf>:й]
NewVerb > Question
NegativeVerb > Question

LEXICON Auxilary # for periphrastic tenses according to kaye
икІо
игъи

LEXICON Class
<cl>:в[1]
<cl>:й[2]
<cl>:б[3]
<cl>:р[4]

#-----------verbal-stems-----------

PATTERN Infinitive
PureInfinitive [<verb>:]
Class ClassInfinitive [<verb>:]
Class (SingularVowel | PluralVowel) ClassVowelInfinitive [<verb>:]

PATTERN Aorist
PureAorist [<verb>:]
Class ClassAorist [<verb>:]
Class (SingularVowel | PluralVowel) ClassVowelAorist [<verb>:]

PATTERN Imperative
PureImperative [<verb>:]
Class ClassImperative [<verb>:]
Class (SingularVowel | PluralVowel) ClassVowelImperative [<verb>:]

LEXICON SingularVowel
<sg>:у
<sg>:и

LEXICON PluralVowel
<pl>:о
<pl>:а

#-----------verbal-patterns-----------

PATTERN NewVerb

Infinitive [<inf>:у]                                # 1 infinitive
Infinitive [<inf>:у] Purposive                      # 2 purposive
Infinitive Habitual                                 # 3 habitual
Infinitive Future                                   # 4 future
Infinitive PresentParticiple                        # 5 present participle
Infinitive [:у] Prospective                         # 6 prospective 

Aorist [<aor>:]                                     # 7 aorist
Aorist Perfect                                      # 8 perfect
Aorist Progressive                                  # 9 progressive
Aorist Progressive PresentParticiple[a]             # 10 present participle
Aorist PastParticiple                               # 11 past participle
Aorist Progressive  PresentConverb                  # 12 present converb
Aorist PastConverb                                  # 13 past converb
Aorist Conditional                                  # 14 conditional
Aorist Concessive                                   # 15 concessive
Aorist PastParticiple Counterfactual[дур]           # 16 counterfactual
Aorist Perfect Counterfactual[|[бдур,ламдур]]
Aorist [<aor>:] Counterfactual[ламдур]
Infinitive Future Counterfactual[|[бдур,ламдур]]
Aorist Masdar                                       # 20 masdar

Imperative [<imp>:]                                 # 17 18 imperative
Imperative [<imp>:] OptativeJussive                 # 19 optative jussive
Aorist [<aor>:] OptativeJussive                     # 19 optative jussive


PATTERN NegativeVerb

Infinitive [<inf>:у] [<neg>:ссу]                    # 1 infinitive
Infinitive [<inf>:у] [<neg>:ссу] Purposive          # 2 purposive
Infinitive Habitual [<neg>:ссу]                     # 3 habitual
Infinitive [<fut><neg>:осса]                        # 4 future
Infinitive [<part.prs><neg>:осса]                   # 5 present participle
Infinitive [:у] Prospective [<neg>:ссу]             # 6 prospective 

Aorist [<aor>:] [<neg>:ссу]                         # 7 aorist
Aorist Perfect [<neg>:ссу]                          # 8 perfect
Aorist Progressive [<neg>:ссу]                      # 9 progressive
Aorist Progressive PresentParticiple[a] [<neg>:ссу] # 10 present participle
Aorist [<neg>:ссу] PastParticiple                   # 11 past participle
Aorist Progressive [<neg>:ссу] PresentConverb       # 12 present converb
Aorist [<cvb.pst><neg>:чIигу]                       # 13 past converb
Aorist [<neg>:ссу] Conditional                      # 14 conditional
Aorist [<neg>:ссу] Concessive                       # 15 concessive
Aorist [<neg>:ссу] PastParticiple Counterfactual[дур]   # 16 counterfactual
Aorist Perfect [<neg>:ссу] Counterfactual[|[бдур,ламдур]]
Aorist [<aor>:] [<neg>:ссу] Counterfactual[ламдур]
Infinitive [<fut><neg>:осса] Counterfactual[|[бдур,ламдур]]
Aorist [<neg>:ссу] Masdar                           # 20 masdar

Imperative [<imp>:] [<neg>:ссу]                     # 17 18 imperative
Imperative [<imp>:] [<neg>:ссу] OptativeJussive     # 19 optative jussive
Aorist [<aor>:] [<neg>:ссу] OptativeJussive         # 19 optative jussive


LEXICON Perfect
<pf>:дду
<pf>:й

LEXICON PastConverb
<cvb.pst>:дду
<cvb.pst>:й

LEXICON PastParticiple
<part.pst>:б
<part.pst>:иб
<part.pst>:м    # заменяет нм -> м в тволе

LEXICON Plural
<pl>:л
<pl>:ил
<pl>:ул

LEXICON Progressive
<progr>:р
<progr>:радо
<progr>:ралло
<progr>:мадо    # заменяет нм -> м в тволе
<progr>:малло   # заменяет нм -> м в тволе

LEXICON PresentConverb
<cvb.prs>:гужа

LEXICON PresentParticiple
<part.prs>:сси[a]
<part.prs>:я
<part.prs>:ия
<part.prs>:йа
<part.prs>:ийа

LEXICON OptativeJussive
<opt>:до

LEXICON Conditional
<cond>:бор
<cond>:мор      # ассимиляция нм -> мм

LEXICON Concessive
<conc>:болло
<conc>:молло    # ассимиляция нм -> мм
<conc>:боло
<conc>:моло     # ассимиляция нм -> мм

LEXICON Counterfactual # в корпусе вообще их почти нет
<ctrf>:дур[дур]
<ctrf>:бдур[бдур]
<ctrf>:ламдур[ламдур]
<ctrf>:ламдури[ламдур]

LEXICON Masdar
<msd>:р         # рн -> н
<msd>:ир

LEXICON Habitual
<hab>:о
<hab>:е

LEXICON Purposive
<purp>:рий

LEXICON Future
<fut>:йа
<fut>:я
<fut>:ийа
<fut>:ия

LEXICON Prospective
<prsp>:лхъи

LEXICON Question
<q>:ле
<q>:де
