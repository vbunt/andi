PATTERNS
Noun
Numeral
AdverbAdjective
Conjunction
Pronoun
Demonstrative
OtherPronoun
Evidentiality
Verb

LEXICON Class
<cl>:в[1]
<cl>:й[2]
<cl>:б[3]
<cl>:р[4]


#-----------VERBS-----------

PATTERN Verb
Inf [<verb>:] VMarkers[finf] # инфинитив 1
Inf [<verb>:] VMarkers[pres] # настоящее
Aor [<verb>:] VMarkers[fut] # будущее но кажется оно тоже должно быть от инфинитива с сокращением основы

LEXICON Aor
гьитІон

LEXICON Imp
гьитІом

LEXICON Inf
гьитІондд # в евангелии
гьитІонн # в словаре кико

LEXICON VMarkers
<finf>:у[finf]
<pres>:о[pres]
<fut>:я[fut]
<fut>:йа[fut]


#-----------NOUNS-----------

PATTERN Noun
AbsSg
AbsPl
OblSg
OblPl

PATTERN AbsSg # абсолютив единственного
NounAbsSgLex [<n>:] [<abs.sg>:]

PATTERN OblSg # косвенные падежи единственного
NounOblSgLex [<n>:] [<obl.sg>:] Case[-pl,-gen,-aff]
GenitiveSg
AffectiveSg

PATTERN GenitiveSg # генитив единственного
NounOblSgLex[1] [<n>:] [<obl.sg>:] Class Gen
NounOblSgLex[-1] [<n>:] [<obl.sg>:] Case[gen]

PATTERN AffectiveSg # аффектив единственного
NounOblSgLex [<n>:] [<obl.sg>:] Class Case[aff]

PATTERN AbsPl
NounAbsPlLex[л] [<n>:] AbsPlSuf[л]
NounAbsPlLex[ил] [<n>:] AbsPlSuf[ил]
NounAbsPlLex[ул] [<n>:] AbsPlSuf[ул]
NounAbsPlLex[ол] [<n>:] AbsPlSuf[ол]
NounAbsPlLex[лил] [<n>:] AbsPlSuf[лил]

PATTERN OblPl
NounOblPlLex [<n>:] [<obl.pl>:] Case[-sg,-gen,-aff]
GenitivePl
AffectivePl

PATTERN GenitivePl # генитив множественного
NounOblPlLex[1] [<n>:] [<obl.pl>:] Class Gen
NounOblPlLex[-1] [<n>:] [<obl.pl>:] Case[gen]

PATTERN AffectivePl # аффектив множественного
NounOblPlLex [<n>:] [<obl.pl>:] Class Case[aff]

LEXICON Case
<erg>:ди[erg]
<dat>:й[dat]
<dat>:лъу[dat]
<dat>:лълъий[dat]
<gen>:лІи[gen]
<gen>:лІол[gen]
<aff>:о[aff]
<ins>:хъи
<sub>:кьи
<sub><el>:кьикку
<sup.ess>:ла
<sup.lat>:ло
<sup><el>:лакку
<in.ess>:ла[sg]
<ess>:у[sg]
<in.lat>:ло[sg]
<lat>:ди[sg]
<in><el>:лакку[sg]
<inter>:лІи
<inter><el>:лІикку
<apud.ess>:ха
<apud.lat>:хо
<apud><el>:хакку
<cont>:чІу
<cont><el>:чІукку
<loc>:хъи[pl]
<loc>:хъи[pl]
<loc><el>:хъикку[pl]
<in>:у[sg]
<in>:у[sg]
<in><el>:укку[sg]

LEXICON AbsPlSuf
<abs.pl>:л[л]
<abs.pl>:ил[ил]
<abs.pl>:ул[ул]
<abs.pl>:ол[ол]
<abs.pl>:лил[лил]

LEXICON Gen
<gen>:ул
<gen>:ил
<gen>:


#-----------ADVERBS AND ADJECTIVES-----------

PATTERN AdverbAdjective
NoClasses > Vowels [<pl>:л]
ClassInfix > Vowels [<pl>:л]
ClassPrefix > Vowels [<pl>:л]

PATTERN NoClasses
AdjNoClassesLex [<adj>:]
AdvNoClassesLex [<adv>:]

PATTERN ClassPrefix
Class[1] AdjStem[1] [<adj>:]
Class[2] AdjStem[2] [<adj>:]
Class[3] AdjStem[3] [<adj>:]
Class[4] AdjStem[4] [<adj>:]
Class[1] AdvStem[1] [<adv>:]
Class[2] AdvStem[2] [<adv>:]
Class[3] AdvStem[3] [<adv>:]
Class[4] AdvStem[4] [<adv>:]

PATTERN ClassInfix
AdjInStem(1) Class AdjInStem(2) [<adj>:]


#-----------EVIDENTIALITY-----------

PATTERN Evidentiality
Evid [<evid>:]

LEXICON Evid # частица мол
гъодду # из евангелия и Салимова
лъоди # из кайе
гъоди # из кайе


#-----------CONJUNCTIONS-----------

PATTERN Conjunction
Conjunctions[-clitic] [<conj>:]
AbsPl Conjunctions[clitic]
AbsPl Conjunctions[clitic]
OblSg Conjunctions[clitic]
OblPl Conjunctions[clitic]

LEXICON Conjunctions # по салимову
я # или
ялъибор # или
во # и # ?ва
амма # но
<conj>:ло[clitic] # и
<conj>:но[clitic] # и


#-----------DEMONSRATIVES-----------

PATTERN Demonstrative
Consonants[х] Vowels (Consonants[н] (Vowels)?)? (Consonants[|[д,г,лъ]] Vowels)? Class [<demonstrative>:] # лъ вместо л-палка

LEXICON Vowels
а
о
у
е
и

LEXICON Consonants
гь[х]
д[д]
г[г]
лъ[лъ]
н[н]


#-----------PERSONAL PRONOUNS-----------

PATTERN Pronoun
Pronouns[sg,-obl] [<pronoun>:] [<abs>:]
Pronouns[pl] [<pronoun>:] [<abs>:л] # в грамматике л не отделяется но я решила что так лучше
Pronouns[sg,-obl] [<pronoun>:] [<erg>:ни]
Pronouns[pl] [<pronoun>:] [<erg>:ди]
Pronouns[sg,obl] [<pronoun>:] Class [<gen>:]
Pronouns[pl] [<pronoun>:] Class [<gen>:]
Pronouns[pl] [<pronoun>:] [<gen>:лълъ]
Pronouns[|[pl,obl]] [<pronoun>:] [<dat>:й]
# iƛi-ɬu здесь должен быть еще такой паттерн
Pronouns[|[pl,obl]] [<pronoun>:] Class [<aff>:о]

LEXICON Pronouns # following kaye
ден[sg] # I
мен[sg] # you sg
бисси[pl] # you pl
ищи[pl] # we excl
илълъи[pl] # we incl / в этом корне в грамматике ƛ, но я пишу его здесь как ɬ потому что сравниваю с евангелием
ди[sg,obl] # I
ду[sg,obl] # you sg


#-----------OTHER PRONOUNS-----------

PATTERN OtherPronoun # kaye + салимов
Pronoun [<int>:гу]
OtherPronouns[abs] [<pronoun>:] Class [<abs>:] > [<pl>:ол]
OtherPronouns[|[erg,pl]] [<pronoun>:] Case[erg]
OtherPronouns[1,-erg] [<pronoun>:] Class [<gen>:] # без мн числа
OtherPronouns[2,-erg] [<pronoun>:] Case[gen]
OtherPronouns[-abs,-erg] [<pronoun>:] Case[dat]
OtherPronouns[-abs,-erg] [<pronoun>:] Class Case[aff]
OtherPronouns[aff] [<pronoun>:] Class Case[aff]
# без местных падежей

LEXICON OtherPronouns
жи[abs]
эн[2,sg]
энщу[1,sg]
энну[1,pl]
энни[2,pl]
энш[1,sg,erg] # можно вместо этого твол 
энд[2,sg,erg] # можно вместо этого твол
энлълъи[aff]


# -----------NUMERALS-----------

PATTERN Numeral
NumRoot[1] [<num>:] Class # 1
(NumRoot[-1,-4] | (Class NumRoot[4])) [<num>:] [<card>:гу] # 2-10
NumRoot[10] [<num>:] [<lnk>:лІи] # -дцать
(NumRoot[-10,-1,-4] | (Class NumRoot[4])) [<num>:] [<ten>:цІол] [<card>:гу] # round number
(NumRoot[-10,-1,-4] | (Class NumRoot[4])) [<num>:] [<ten>:цІол] [<lnk>:ло] # round +

LEXICON NumRoot
се[1] # 1
чІе # 2 
лъоб # 3
окъго[4] # 4
иншду # 5 
онлІи # 6 
гьокьу # 7
бийкьи # 8
гьочІо # 9
гьоцІо[10] # 10
